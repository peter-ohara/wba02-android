apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

ext {
    supportLibraryVersion = '25.2.0'
    retrofitVersion = '2.1.0'
    versionName = '0.3.0'
    firebaseVersion = '10.2.0'
    firebaseUiVersion = '1.2.0'
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.pascoapp.wba02_android"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 4
        versionName '0.3.0'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "BASE_URL", '"https://wba02-server.herokuapp.com/android_v1/"'
        resValue "string", "app_name", '"Pasco (Staging)"'
        resValue "string", "action_about", "\"Pasco (Staging) $versionName \""
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            buildConfigField "String", "BASE_URL", '"http://10.0.2.2:3000/android_v1/"'
            resValue "string", "app_name", '"Pasco (Debug)"'
            resValue "string", "action_about", "\"Pasco (Debug) $versionName \""
        }
        staging {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationIdSuffix '.staging'
            buildConfigField "String", "BASE_URL", '"https://wba02-server.herokuapp.com/android_v1/"'
            resValue "string", "app_name", '"Pasco (Staging)"'
            resValue "string", "action_about", "\"Pasco (Staging) $versionName \""
            signingConfig signingConfigs.config
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationIdSuffix '.release'
            buildConfigField "String", "BASE_URL", '"https://wba02-server-production.herokuapp.com/android_v1/"'
            resValue "string", "app_name", '"Pasco"'
            resValue "string", "action_about", "\"Pasco (Release) $versionName \""
            signingConfig signingConfigs.config
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

repositories {
    maven { url = "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'

    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"

    compile "com.android.support:support-v4:$supportLibraryVersion'"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"

    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile "com.android.support:cardview-v7:$supportLibraryVersion"

    compile "com.android.support:preference-v7:$supportLibraryVersion"

    // Firebase Core
    compile "com.google.firebase:firebase-core:$firebaseVersion"

    // Firebase Realtime Database
    compile "com.google.firebase:firebase-database:$firebaseVersion"

    // Firebase Notifications
    compile "com.google.firebase:firebase-messaging:$firebaseVersion"

    // Firebase Crash Reporting
    compile "com.google.firebase:firebase-crash:$firebaseVersion"

    // Firebase Auth
    compile "com.google.firebase:firebase-auth:$firebaseVersion"
    compile "com.google.android.gms:play-services-auth:$firebaseVersion"

    //Firebase Invites
    compile "com.google.firebase:firebase-invites:$firebaseVersion"

    // FirebaseUI Auth
    compile "com.firebaseui:firebase-ui-auth:$firebaseUiVersion"

    // FirebaseUI Database
    compile "com.firebaseui:firebase-ui-database:$firebaseUiVersion"

    // Library for colored Square Icons with letter inside like in gmail
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    // RxAndroid and RxJava
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'

    // And ProGuard rules for RxJava!
    compile 'com.artemzin.rxjava:proguard-rules:1.2.4.0'

    // View Injection
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    // Lombok
    provided "org.projectlombok:lombok:1.16.10"

    // Chunk Templating Engine
    compile "com.x5dev:chunk-templates:3.2.3"

    // AVloadingIndicator
    compile 'com.wang.avi:library:2.1.3'

    // EazeGraph charting library
    compile 'com.github.blackfizz:eazegraph:1.2.5l@aar'
    compile 'com.nineoldandroids:library:2.4.0'

    // REST Interactions
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
    compile "com.squareup.okhttp3:logging-interceptor:3.6.0"

    // JSON Parsing
    compile 'com.google.code.gson:gson:2.6.1'
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Logging
    compile 'com.jakewharton.timber:timber:4.5.1'
}

apply plugin: 'com.google.gms.google-services'
